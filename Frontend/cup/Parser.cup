/*
  CUP Parser
*/

import java.util.*;
import java.io.*;
import java_cup.runtime.Symbol;
import java_cup.runtime.*;
import edu.tum.lua.ast.*;


/* Preliminaries to use the scanner.  */
scan with {: return lexer.next_token(); :};


parser code {: 
  public boolean syntaxErrors;


  Lexer lexer;

  public Parser(Lexer lex) {
  		
      super(lex);
      lexer = lex;
     
  }

  
  public void report_error(String message, Object info) {
    syntaxErrors = true;
    
    System.out.print(message);
    
    if ( !(info instanceof Symbol) ) return;
    Symbol symbol = (Symbol) info;
    
    if ( symbol.left < 0 || symbol.right < 0 ) return;
    
    System.out.println(" at line "+symbol.left+", column "+symbol.right);
  }
:}; 





 /* Terminals (tokens returned by lexer). */


terminal         ASM; //Assigment =
terminal		 DOT, DDOT;
terminal		 LOCAL, FUNC, END;
terminal  		 DO, WHILE, FOR, IN, REPEAT, UNTIL;
terminal         IF, THEN, ELSE, ELSEIF, RETURN, BREAK;
terminal         NIL, FALSE, TRUE, PARAMS;
terminal         ADD, MUL, SUB, DIV, MOD, POW, CONCAT;
terminal         EQ, LEQ ,NEQ , LE, GR, GEQ, AND, OR; 
terminal         NOT, LENGTH;
terminal 		 LPAREN, RPAREN, LBRACK, RBRACK, LCURL, RCURL;
terminal		 SEMI, COM, WS;
terminal Double NUMBER;
terminal String  ID, TEXT;




 /*NON TERMINALS */
non terminal Chunk chunk;
non terminal Block block, optelse ;
non terminal Stat stat;
non terminal StatList statlist;
non terminal LastStat laststat;

non terminal FuncName funcname;
non terminal IfThenElse ifthenelse;
non terminal VarList varlist;
non terminal Var var;
non terminal NameList namelist;
non terminal ExpList explist,args;
non terminal Exp exp, optforexp;
non terminal PrefixExp prefixexp;
non terminal FunctionCall functioncall;
non terminal Function function;
non terminal FuncBody funcbody;
non terminal ParList parlist;
non terminal TableConstructor tableconstructor;
non terminal FieldList fieldlist;
non terminal Field field;
non terminal fieldsep;
non terminal Integer binop;
non terminal Integer unop;
non terminal optsemi, empty, optws, optasm,  optdotname, optcolonname, optvarlist;

precedence left ADD, MUL, SUB, DIV, MOD, POW, CONCAT, EQ, LEQ ,NEQ , LE, GR, GEQ, AND, OR;
precedence left LPAREN, RPAREN;
/* Grammar */

start with block;


block 			::=     statlist:stats laststat:lstat
                 		{:       
                 			if(lstat == null)   {   
                 			System.out.println("Inside block");    	    
                 			RESULT = new Block(stats,null); 	}
                 			else	{
                 			System.out.println("Inside block with laststat");   
                 			RESULT = new Block(stats,lstat);  }
                 		:}
                 		| empty {: System.out.println("Inside empty block"); RESULT = new Block(null,null);; :}
                 		;

	

statlist		::=		stat:s optsemi optws
						{: 	
							System.out.println("Inside single stat"); 			
							RESULT = new StatList(s);
						:}							
						
						|  stat:s  optsemi optws statlist:sl 
						{:						
					   		if(sl== null){
					    	System.out.println("Inside recu stat"); 	
					    	sl =new StatList(s);
							RESULT = sl;}
							else{
							System.out.println("Inside recu stat");
							sl.insertElementAt(s,0);
							RESULT = sl;
							}
						:}	
							
						;
					
						


					
stat        	::=    		varlist:vl ASM explist:el 
						{: 
								System.out.println("Inside stat");
								RESULT = new Asm(vl,el);
						:}
						| functioncall:fc
						{:
						  RESULT = new FuncCallStmt(fc);
						:}
						
						| 	DO optws block:b END 
						{:    
								System.out.println("Inside DO END");
								RESULT = new DoExp(b); 
						:}
						|  	WHILE optws exp:e  optws DO optws block:b END
						{: 		RESULT = new WhileExp(e,b); :}
						|  	REPEAT optws block:b  UNTIL optws exp:e
						{: 		RESULT = new RepeatUntil(b,e); :}
						|  	IF optws exp:e optws THEN optws block:b optelse:elseblock END
						{: 	RESULT = new IfThenElse(e, b, elseblock);
						:}
						| 	FOR optws ID:id optws ASM optws exp:e1 optws COM optws exp:e2  optforexp:e3 optws DO optws block:b END
						{: 		
								RESULT = new ForExp(id,e1,e2,e3,b); 
						:}
						| FOR optws namelist:nl  IN optws explist:el optws DO optws block:b END
						{: RESULT = new ForIn(nl,el,b); :}
						| FUNC funcname funcbody  
		 				| LOCAL FUNC function ID:id funcbody 
						| LOCAL namelist:nl optasm explist:el
						{: RESULT = new LocalDecl(nl,el); :}
						;
	
laststat		::=		RETURN optws optsemi
						{:    
							System.out.println("Inside laststat");
							RESULT = new LastReturn(null); 
						:}
						|	RETURN optws explist:el optws optsemi	
						{:    
							System.out.println("Inside laststat");
							RESULT = new LastReturn(el); 
						:}
						| BREAK optws optsemi{: RESULT = new LastBreak(); :}	
						| BREAK optws optsemi statlist{: RESULT = new LastBreak(); :}
						| empty
						; 						
funcname		::=     ID:id
						| ID:id DOT funcname
						| DDOT ID:id
						;
						

optelse			::= 	ELSE optws block:b {: RESULT = b; :}						
						| empty
						;	
												
optforexp		::=		COM optws exp:e {: RESULT = e; :}				
						| empty {: RESULT = new NumberExp(1.0); :}
						;

varlist			::=     var:v
						{: 							
						RESULT= new VarList(v);
						:}
						| var:v COM varlist:vl
						{: 
						if(vl==null){
						vl = new VarList(v);
						RESULT = vl; }
						else {
						vl.insertElementAt(v,0);
						RESULT = vl; }
						:}								
						;
	
				
explist			::=     exp:e  
							{: 
							RESULT= new ExpList(e);
							:}
						| exp:e COM explist:el 
							{:
								if(el == null) {
									el = new ExpList(e);
								RESULT = el;} 
								else {
									el.insertElementAt(e,0);
								RESULT = el;}
	
							:}
							
						;
						
			
namelist    	::=    	ID:id  
						{: RESULT = new NameList(new Name(id)); :}  
						/* | ID:id optws COM namelist:nl
						{: 	if(nl== null){					    		
					    	nl =new NameList(new Name(id));
							RESULT = nl;}
							else{		
							nl.insertElementAt(new Name(id),0);
							RESULT = nl;
							} 
						:}
							*/										
						;
					
					

var				::=		ID:id
						{:  						
						RESULT =  new Variable(id);						
						:}	
						| prefixexp:pe LBRACK exp:e RBRACK	
						{:  						
						RESULT =  new VarTabIndex(pe,e);						
						:}					
						;

					
exp				::=		NIL
							{: RESULT = new Nil(); :}  
						| FALSE
							{: RESULT = new BooleanExp(false); :}  
						| TRUE
							{: RESULT = new BooleanExp(true); :} 
						| NUMBER:num
							{: 					   
								RESULT = new NumberExp(num); 						
							:} 
						| TEXT:t 
							{: RESULT = new TextExp(t); :}
						| prefixexp:prefixexp
							{: RESULT = new PreExp(prefixexp); :}
						| tableconstructor:tc
								{: RESULT = new TableConstructorExp(tc); :} 
						| exp:e1 binop:op exp:e2
							{: RESULT = new Binop(e1,op,e2); :}
						| unop:uop exp:e
							{: RESULT = new Unop(uop,e); :}
						| function:f
							{: RESULT = new FunctionExp(f); :}
						;	
	
					
prefixexp		::=		var:v
						{:
							RESULT = new PrefixExpVar(v);
						:}
						| functioncall:fc {:
							System.out.println("inside prefixexp");
							RESULT =  new PrefixExpFuncCall(fc);
						:}
						/*| LPAREN exp:e RPAREN
						{:
							RESULT = new PrefixExpExp(e);
						:}*/
						;
	
	
functioncall	::= 	prefixexp:pexp args:ar
						{:
							System.out.println("inside functioncall");
							RESULT = new FuncCall(pexp,ar);
						:}
						| prefixexp:pexp DDOT TEXT:nam args:ar
						{:
							RESULT = new FuncCallSelf(pexp,new Name(nam),ar);
						:} 
						;

				
args			::= 	LPAREN explist:el RPAREN
						{:
							RESULT = el;
						:}
						| LPAREN RPAREN {: RESULT = new ExpList(null); :}
						| tableconstructor:tc {: RESULT = new ExpList(new TableConstructorExp(tc)); :}
						| TEXT:txt {: RESULT = new ExpList(new TextExp(txt)); :} 
						;

function 		::=     FUNC optws funcbody:fb
						{:
							RESULT = new Function(fb);
						:}
						;
						

funcbody		::= 	LPAREN parlist:pl RPAREN block:bl END
						{:
								RESULT = new FuncBody(pl,bl);
						:}
						;
					

parlist			::=		namelist:nl
						{:
							RESULT = new ParList(nl,false);
						:}
						| namelist:nl COM PARAMS
						{:
							RESULT = new ParList(nl,true);
						:}
						| PARAMS
						{:
							RESULT = new ParList(null,true);
						:}
						| {:
							RESULT = new ParList(null, false);
						  :}
						;
	

tableconstructor ::=    LCURL fieldlist:fl RCURL
 						{: RESULT = new TableConstructor(fl); :} 
 						; 

 						
fieldlist		 ::= 	| field:f
							{: RESULT = new FieldList(f); :}
						| field:f fieldsep fieldlist:fl
							{: if(fl==null) {
									RESULT = new FieldList(f);
								}
								else
								{
									fl.insertElementAt(f,0);
									RESULT = fl;
								}						
							:}
 						;
 						
 						
field			::=		LBRACK exp:e1 RBRACK ASM exp:e2
						{:
						RESULT = new FieldLRExp(e1,e2);
						:}	
						| ID:ident ASM exp:e
						{:
						RESULT = new FieldNameExp(ident,e);
						:}
						| exp:e
						{:
						RESULT = new FieldExp(e);
						:}
						
						;
						

fieldsep		::=		COM | SEMI						
						;		
binop			::=		ADD
						{: RESULT = Op.ADD; :}
						| SUB
						{: RESULT = Op.SUB; :}
						| MUL
						{: RESULT = Op.MUL; :}
						| DIV
						{: RESULT = Op.DIV; :}
						| MOD
						{: RESULT = Op.MOD; :}
						| POW
						{: RESULT = Op.POW; :}
						| CONCAT
						{: RESULT = Op.CONCAT; :}
						| EQ
						{: RESULT = Op.EQ; :}
						| LEQ
						{: RESULT = Op.LE; :}
						| NEQ
						{: RESULT = Op.NEQ; :}
						| LE
						{: RESULT = Op.LT; :}
						| GR
						{: RESULT = Op.GT; :}
						| GEQ
						{: RESULT = Op.GE; :}
						| AND
						{: RESULT = Op.AND; :}
						| OR
						{: RESULT = Op.OR; :}
						;
						
unop			::=		SUB
						{: RESULT = Op.UNM; :}
						| NOT
						{: RESULT = Op.NOT; :}
						| LENGTH
						{: RESULT = Op.LEN; :}
						;
								
optasm			::= 	ASM	  | empty;
						
optsemi			::= 	SEMI  | empty ;


optws			::= 	WS | empty ;
						
empty			::=		;

